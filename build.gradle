plugins {
  id 'java'
  id 'io.quarkus'
  id 'com.adarshr.test-logger' version '2.1.0'
  id 'com.github.davidmc24.gradle.plugin.avro' version '1.2.0'
}

apply from: 'buildscripts/code-analysis.gradle'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
  maven {
    url "https://packages.confluent.io/maven/"
  }
}

sourceSets {
  integrationTest {
    java.srcDir file('src/integrationTest/java')
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

task integrationTest(type: Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  shouldRunAfter test
}

check.dependsOn integrationTest

configurations {
  integrationTestImplementation.extendsFrom implementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.setCanBeResolved(true)
  integrationTestRuntimeOnly.setCanBeResolved(true)
}




// Add generated avro sources to main source sets
sourceSets.main.java.srcDirs = ['build/generated-main-avro-java', 'src/main/java', 'build/generated/sources/annotationProcessor/java/main']

// Fix: com.datastax.oss:* uses old, mandatory Groovy version
configurations.all {
  resolutionStrategy.force group: 'org.codehaus.groovy', name: 'groovy-json', version: '2.5.7'
  resolutionStrategy.force group: 'org.codehaus.groovy', name: 'groovy', version: '2.5.7'
}

dependencies {
  implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")

  implementation 'io.quarkus:quarkus-container-image-jib'

  // Base
  implementation 'io.quarkus:quarkus-resteasy'
  implementation 'io.quarkus:quarkus-scheduler'
  implementation 'io.quarkus:quarkus-resteasy-jackson'
  implementation 'io.quarkus:quarkus-smallrye-openapi'
  implementation 'io.quarkus:quarkus-resteasy-mutiny'

  // Cassandra
  annotationProcessor group: 'com.datastax.oss', name: 'java-driver-mapper-processor', version: '4.10.0'
  implementation group: 'com.datastax.oss', name: 'java-driver-mapper-runtime', version: '4.10.0'
  implementation 'com.datastax.oss.quarkus:cassandra-quarkus-client'
  testImplementation 'com.datastax.oss.quarkus:cassandra-quarkus-test-framework'

  // Kafka
  implementation 'io.quarkus:quarkus-kafka-streams'
  implementation 'io.quarkus:quarkus-kafka-client'
  implementation group: 'io.confluent', name: 'kafka-avro-serializer', version: '5.5.0'
  implementation group: 'io.confluent', name: 'kafka-streams-avro-serde', version: '5.5.0'
  implementation 'io.quarkus:quarkus-avro'
  implementation group: 'io.quarkus', name: 'quarkus-smallrye-reactive-messaging-kafka'

  // Utility
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
  implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'

  // Tests
  testImplementation 'io.quarkus:quarkus-junit5'
  testImplementation "org.mockito:mockito-core:2.+"
  testImplementation 'io.rest-assured:rest-assured'
  testImplementation "org.testcontainers:kafka:1.15.2"

  // Integration Testing
  integrationTestImplementation 'com.datastax.oss.quarkus:cassandra-quarkus-test-framework'
  integrationTestImplementation "org.testcontainers:kafka:1.15.2"
  integrationTestImplementation 'io.quarkus:quarkus-junit5'
  integrationTestImplementation 'org.mockito:mockito-junit-jupiter'
  integrationTestImplementation group: 'org.apache.kafka', name: 'kafka-streams-test-utils', version: '2.4.1'


}

group 'net.explorviz'
version '1.0.0-SNAPSHOT'

compileJava {
  options.encoding = 'UTF-8'
  options.compilerArgs << '-parameters'
}

compileTestJava {
  options.encoding = 'UTF-8'
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

test {
  systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}
